PREFIX mod: <http://purl.org/webofneeds/modification#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX msg: <http://purl.org/webofneeds/message#>
PREFIX won: <http://purl.org/webofneeds/model#>
PREFIX rdfg: <http://www.w3.org/2004/03/trix/rdfg-1/>

DELETE {
  GRAPH ?contentGraph { ?s ?p ?o }.
}
WHERE
{
  {
    # remove all messages that have remote messages (except responses)
    GRAPH <urn:x-arq:UnionGraph> {
      ?m msg:hasContent ?contentGraph;
         msg:hasMessageType ?mtype;
         msg:hasCorrespondingRemoteMessage ?rm .
         filter (?mtype in (msg:ConnectMessage, msg:OpenMessage, msg:CloseMessage, msg:ConnectionMessage))
         # unless there are the required SuccessResponses
        FILTER NOT EXISTS {
          ?sm msg:isResponseTo ?m.
          ?sm msg:hasPreviousMessage+ ?m.
          ?sm msg:hasMessageType msg:SuccessResponse.
          ?rsm msg:isRemoteResponseTo ?m.
          ?rsm msg:isResponseTo ?rm.
          ?rsm msg:hasPreviousMessage+/msg:hasCorrespondingRemoteMessage ?m.
          ?rsm msg:hasMessageType msg:SuccessResponse.
          ?rsm msg:hasCorrespondingRemoteMessage ?lsm.
          ?lsm msg:hasPreviousMessage+ ?m.
        }
    }
  } union {
    # remove all messages that do not require remote messages
    GRAPH <urn:x-arq:UnionGraph> {
      ?m msg:hasContent ?contentGraph;
         msg:hasMessageType ?mtype.
      filter (?mtype  in (msg:CreateMessage, msg:ActivateMessage, msg:DeactivateMessage, msg:HintFeedbackMessage))
      # unless there is the required SuccessResponse
      FILTER NOT EXISTS {
        ?sm msg:isResponseTo ?m.
        ?sm msg:hasPreviousMessage+ ?m.
        ?sm msg:hasMessageType msg:SuccessResponse.
      }
    }
  } union {
    # remove all messages that require remote but don't have any (except responses)
    GRAPH <urn:x-arq:UnionGraph> {
      ?m msg:hasContent ?contentGraph;
         msg:hasMessageType ?mtype;
         filter (?mtype  in (msg:ConnectMessage, msg:OpenMessage, msg:CloseMessage, msg:ConnectionMessage))
         optional {
           ?m msg:hasCorrespondingRemoteMessage ?rm .
         }
         filter (!bound(?rm))
    }
  }
  GRAPH ?contentGraph {
      ?s ?p ?o .
  }
}