PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX msg: <http://purl.org/webofneeds/message#>
PREFIX won: <http://purl.org/webofneeds/model#>
PREFIX agr: <http://purl.org/webofneeds/agreement#>
PREFIX mod: <http://purl.org/webofneeds/modification#> 

SELECT distinct ?msg ?timestamp $distance ?msgType ?text ?senderNeed ?retracts ?accepts ?proposes ?proposesToCancel WHERE {
 {
 {
   SELECT distinct ?first ?msg (count (?mid) as ?distance) WHERE {
    GRAPH <urn:x-arq:UnionGraph> {
     ?msg msg:hasCorrespondingRemoteMessage?/msg:hasPreviousMessage* ?mid .
     ?mid msg:hasPreviousMessage* ?first .
     ?first msg:hasSenderNeed ?senderOfFirstMessage .
     FILTER NOT EXISTS {?first msg:hasPreviousMessage ?none}
     }            
   }
   GROUP BY ?msg ?first 
 }
 GRAPH <urn:x-arq:UnionGraph> {
   ?msg msg:hasSenderNeed ?senderNeed.
   ?msg msg:hasMessageType ?msgType.
   ?msg msg:hasReceivedTimestamp ?timestamp.
   OPTIONAL {
    ?msg won:hasTextMessage ?text.
   }
   OPTIONAL {
    ?msg agr:proposes ?proposes.
   }
   OPTIONAL {
    ?msg agr:proposesToCancel ?proposesToCancel.
   }
   OPTIONAL {
    ?msg agr:accepts ?accepts.
   }
   OPTIONAL {
    ?msg mod:retracts ?retracts.
   }
 }
 }  
} 